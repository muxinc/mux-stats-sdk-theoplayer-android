apply plugin: 'com.android.library'
apply plugin: 'checkstyle'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'com.mux.gradle.android.mux-android-distribution'

android {
  compileSdkVersion project.ext.compileSdkVersion

  defaultConfig {
    minSdkVersion project.ext.minSdkVersion
    targetSdkVersion project.ext.targetSdkVersion

    multiDexEnabled true

    buildConfigField("String", "MUX_PLUGIN_VERSION", "\"${versionName}\"")
    buildConfigField("String", "MUX_PLUGIN_NAME", "\"android-theoplayer-mux\"")

    testInstrumentationRunner "androidx.support.test.runner.AndroidJUnitRunner"
  }


  buildTypes {
    debug {
      minifyEnabled false
    }
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  flavorDimensions 'api'

  productFlavors {
    v5 {
      dimension 'api'
      minSdkVersion 21
    }
  }

  compileOptions {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
  }
  lint {
    abortOnError false
  }
  namespace 'com.mux.stats.sdk.muxstats.theoplayer'
}

muxDistribution {
  devVersion versionFromCommitHash('dev-')
  releaseVersion versionFromTag()
  artifactIds artifactFromFlavorValue('api')
  groupIds just("com.mux.stats.sdk.muxstats")
  publishIf { it.containsIgnoreCase("release") }
  publicReleaseIf releaseOnTag()

  // TODO: This is kinda clumsy, packageJavadocs should be a function not a property probably
  packageJavadocs = releaseOnTag().call()
  artifactoryConfig {
    contextUrl = "https://muxinc.jfrog.io/artifactory/"
    releaseRepoKey = 'default-maven-release-local'
    devRepoKey = 'default-maven-local'
  }
}

dependencies {
  implementation 'com.android.support:multidex:1.0.3'
  //noinspection GradleDynamicVersion // THEO claims there will be no breaking changes.
  v5Api 'com.theoplayer.theoplayer-sdk-android:core:5.9.1'
  //noinspection GradleDependency,GradleDynamicVersion // Old kotlin asked for by name in docs
  v5Api 'org.jetbrains.kotlin:kotlin-stdlib:1.6.+'

  compileOnly 'com.google.ads.interactivemedia.v3:interactivemedia:3.31.0'
  compileOnly 'com.google.android.gms:play-services-ads-identifier:18.0.1'
  testImplementation 'junit:junit:4.13.2'

  api "com.mux:stats.muxcore:${project.ext.muxCoreVersion}"
}

