apply plugin: 'com.android.library'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "0.0.1"

        buildConfigField("String", "MUX_PLUGIN_VERSION", "\"${versionName}\"")
        buildConfigField("String", "MUX_PLUGIN_NAME", "\"android-theoplayer-mux\"")

        testInstrumentationRunner "androidx.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions 'api'

    productFlavors {
        minapi16 {
            dimension 'api'
        }
        minapi21 {
            dimension 'api'
            minSdkVersion 21
        }
        minapi16_ads {
            dimension 'api'
        }
        minapi21_ads {
            dimension 'api'
            minSdkVersion 21
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    build  {
        doLast {
            productFlavors.each {
                flavor ->
                    def versionName = flavor.name.replaceAll(/_/, ".")
                    if (!versionName.endsWith(".ads")) {
                        copy {
                            from "${buildDir}/outputs/aar/${project.name}-${flavor.name}-release.aar", "${buildDir}/outputs/mapping/${flavor.name}Release/mapping.txt"
                            into "${buildDir}/outputs/artifacts/"
                            rename("${project.name}-${flavor.name}-release.aar", "mux-${versionName}.aar")
                            rename("mapping.txt", "mapping-${versionName}.txt")
                        }
                    }
            }
            dependencies {}
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api files('libs/MuxCore.jar')
    minapi16CompileOnly(name: 'theoplayer-minapi16', ext: 'aar')
    minapi16_adsCompileOnly(name: 'theoplayer-minapi16', ext: "aar")
    minapi21CompileOnly(name: 'theoplayer-minapi21', ext: "aar")
    minapi21_adsCompileOnly(name: 'theoplayer-minapi21', ext: "aar")
    compileOnly 'com.google.ads.interactivemedia.v3:interactivemedia:3.18.0'
    compileOnly 'com.google.android.gms:play-services-ads-identifier:17.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
