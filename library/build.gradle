apply plugin: 'com.android.library'
apply plugin: 'checkstyle'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'com.mux.gradle.android.mux-android-distribution'

android {
  compileSdk project.ext.compileSdkVersion

  defaultConfig {
    minSdkVersion project.ext.minSdkVersion
    targetSdkVersion project.ext.targetSdkVersion

    multiDexEnabled true

    buildConfigField("String", "MUX_PLUGIN_VERSION", "\"${versionName}\"")
    buildConfigField("String", "MUX_PLUGIN_NAME", "\"android-theoplayer-mux\"")

    testInstrumentationRunner "androidx.support.test.runner.AndroidJUnitRunner"
  }

  buildFeatures {
    buildConfig true
  }

  buildTypes {
    debug {
      minifyEnabled false
    }
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  flavorDimensions 'api'

  productFlavors {
    v7 {
      dimension 'api'
      minSdkVersion 21
    }
  }

  compileOptions {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
  }
  lint {
    abortOnError false
  }
  namespace 'com.mux.stats.sdk.muxstats.theoplayer'
}

muxDistribution {
  devVersion versionFromCommitHash('dev-')
  releaseVersion versionFromTag()
  artifactIds just("muxstatssdktheoplayer_v7")
  groupIds just("com.mux.stats.sdk.muxstats")
  publishIf { it.containsIgnoreCase("release") }
  publicReleaseIf releaseIfCmdFlag("publicRelease")

  dokkaConfig {
    moduleName = "Mux Data SDK for THEOplayer"
    footer = "(c) " + new Date().format("yyyy") + " Mux, Inc. Have questions or need help?" +
            " Contact support@mux.com"
  }

  // TODO: This is kinda clumsy, packageJavadocs should be a function not a property probably
  packageJavadocs = releaseIfCmdFlag("publicRelease").call()
  artifactoryConfig {
    contextUrl = "https://muxinc.jfrog.io/artifactory/"
    releaseRepoKey = 'default-maven-release-local'
    devRepoKey = 'default-maven-local'
  }
}

dependencies {
  implementation 'com.android.support:multidex:1.0.3'
  //noinspection GradleDynamicVersion // THEO claims there will be no breaking changes.
  v7Api "com.theoplayer.theoplayer-sdk-android:core:${project.ext.theoplayerVersion}"
  v7Api "com.theoplayer.theoplayer-sdk-android:integration-ads-ima:${project.ext.theoplayerVersion}"

  testImplementation 'junit:junit:4.13.2'

  api "com.mux:stats.muxcore:${project.ext.muxCoreVersion}"
}

